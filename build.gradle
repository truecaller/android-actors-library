/*
 * Copyright (C) 2017 True Software Scandinavia AB
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.truecaller.gradle.SdkLocator

File sdkLocationDir = SdkLocator.findSdkLocation(projectDir)
String sdkLocation
if (sdkLocationDir == null) {
    throw new RuntimeException("Setup android SDK location in one of the following ways: ANDROID_HOME env variable, " +
            "android.home system property or in local.properties file")
} else {
    sdkLocation = sdkLocationDir.absolutePath
}

project.ext["androidJar"] = fileTree(dir: "${sdkLocation}/platforms/android-26/", include: ['android.jar'])

buildscript {
    repositories {
        jcenter()
    }
}

allprojects {
    group = 'com.truecaller'
    version = "1.6.1"

    if (!project.hasProperty("build.isRelease")) {
        version = "$version-SNAPSHOT"
    }

    repositories {
        mavenLocal()
        maven { url "file://${sdkLocation}/extras/android/m2repository/" }
        jcenter()
        mavenCentral()
        google()
    }

    // Workaround for http://issues.gradle.org/browse/GRADLE-1682
    tasks.withType(Test) {
        scanForTestClasses = false
        include "**/*Test.class" // whatever Ant pattern matches your test class files
    }
}
