/*
 * Copyright (C) 2017 True Software Scandinavia AB
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: "jacoco"

sourceCompatibility = 1.7

dependencies {
    implementation project(':actors-library')
    implementation rootProject.androidJar
    // https://mvnrepository.com/artifact/com.intellij/annotations
    implementation group: 'com.intellij', name: 'annotations', version: '12.0'
    // https://mvnrepository.com/artifact/com.squareup/javapoet
    implementation group: 'com.squareup', name: 'javapoet', version: '1.9.0'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    implementation group: 'androidx.annotation', name: 'annotation', version: '1.1.0'
    testImplementation group: 'junit', name: 'junit', version: '4.11'
    // https://mvnrepository.com/artifact/com.google.testing.compile/compile-testing
    testImplementation group: 'com.google.testing.compile', name: 'compile-testing', version: '0.10'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

ext {
    artifactId = 'android-actors-generator'
    artifactName = 'Annotation processor for Truecaller Android Actors Library'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                "com/truecaller/androidactors/ModelFactoryImpl.class",
                "com/truecaller/androidactors/ActorGeneratedModel.class",
                "com/truecaller/androidactors/ActorParsedModel.class",
                "com/truecaller/androidactors/GenerationError.class",
            ])
        })
    }
}

apply from: '../upload.gradle'
