/*
 * Copyright (C) 2017 True Software Scandinavia AB
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: "jacoco"

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.robolectric', name: 'robolectric', version: '3.4.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
    testImplementation rootProject.androidJar

    implementation group: 'androidx.annotation', name: 'annotation', version:'1.1.0'
    compileOnly rootProject.androidJar
}


test {
    jacoco {
        includeNoLocationClasses = true
    }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                "com/truecaller/androidactors/ActorsPackage**",
                "com/truecaller/androidactors/Promise.class"
            ])
        })
    }
}

ext {
    artifactId = 'android-actors-library'
    artifactName = 'Truecaller Android Actors Library'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

apply from: '../upload.gradle'
